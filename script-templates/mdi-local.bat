ECHO OFF
SETLOCAL EnableDelayedExpansion
REM -----------------------------------------------------------------------
REM launch the Michigan Data Interface in local mode on Windows
REM     web server  runs on a user's local desktop/laptop computer
REM     web browser runs on a user's local desktop/laptop computer
REM -----------------------------------------------------------------------
REM this script was generated by https://wilsonte-umich.shinyapps.io/mdi-script-generator/
REM GitHub repository for script generator: https://github.com/MiDataInt/mdi-script-generator
REM script template author: Thomas E. Wilson, https://wilsonte-umich.github.io/
REM -----------------------------------------------------------------------

REM -----------------------------------------------------------------------
REM set variable values
REM -----------------------------------------------------------------------
SET MDI_DIRECTORY=__MDI_DIRECTORY__
SET HOST_DIRECTORY=__HOST_DIRECTORY__
SET DATA_DIRECTORY=__DATA_DIRECTORY__
SET R_DIRECTORY=__R_DIRECTORY__
SET INSTALL_PACKAGES=__INSTALL_PACKAGES__
SET SHINY_PORT=__SHINY_PORT__
SET DEVELOPER=__DEVELOPER__

REM -----------------------------------------------------------------------
REM prompt the user for the requested action
REM -----------------------------------------------------------------------
:USER_PROMPT
SET ACTION_NUMBER=
ECHO.
ECHO Welcome to the Michigan Data Interface.
ECHO.
ECHO   MDI_DIRECTORY    %MDI_DIRECTORY%
ECHO   HOST_DIRECTORY   %HOST_DIRECTORY%
ECHO   DATA_DIRECTORY   %DATA_DIRECTORY%
ECHO   R_DIRECTORY      %R_DIRECTORY%
ECHO   DEVELOPER        %DEVELOPER%
ECHO.
ECHO What would you like to do?
ECHO.
ECHO   1 - run the MDI web interface locally
ECHO   2 - (re)install the MDI on your computer
ECHO   3 - exit and do nothing
ECHO.
SET /p ACTION_NUMBER=Select an action by its number: 

REM -----------------------------------------------------------------------
REM parse and confirm the requested action
REM -----------------------------------------------------------------------
IF "!ACTION_NUMBER!"=="1" (
    SET COMMAND=run
    SET OPTIONS=dataDir=%DATA_DIRECTORY%, port=%SHINY_PORT%, debug=%DEVELOPER%, developer=%DEVELOPER%
    SET MESSAGE=MDI shutdown complete
) ELSE IF "!ACTION_NUMBER!"=="2" (
    SET IP_MESSAGE=-
    IF %INSTALL_PACKAGES%==TRUE (
        SET IP_MESSAGE=- install or update R packages
    )
    ECHO.
    ECHO ------------------------------------------------------------------
    ECHO PLEASE CONFIRM MDI INSTALLATION ACTIONS
    ECHO ------------------------------------------------------------------
    ECHO.
    ECHO   - install the R MDI manager package
    ECHO   - populate directory %MDI_DIRECTORY%
    ECHO   - clone or update MDI repositories from GitHub
    ECHO   - check out the most recent version of all definitive MDI repositories
    ECHO   !IP_MESSAGE!
    ECHO.
    SET /p CONFIRMATION=Do you wish to continue? [type 'y' for 'yes']: 
    IF "!CONFIRMATION!"=="y" (
        SET COMMAND=install
        SET OPTIONS=installPackages=%INSTALL_PACKAGES%, confirm=FALSE
        SET MESSAGE=MDI installation complete
    ) ELSE (
        GOTO USER_PROMPT
    )
) ELSE (
    ENDLOCAL
    EXIT
)

REM -----------------------------------------------------------------------
REM parse the appropriate R command and library paths
REM typical R_DIRECTORY on Windows is C:/Program Files/R/R-4.1.2
REM -----------------------------------------------------------------------
IF "%R_DIRECTORY%"=="NULL" (
    SET RSCRIPT=Rscript.exe
    SET LIB_PATH=%HOMEDRIVE%%HOMEPATH%\mdi-R-library\unspecified
) ELSE (
    SET RSCRIPT=%R_DIRECTORY%/bin/Rscript.exe
    FOR /F "tokens=2 delims=-" %%b in ("%R_DIRECTORY%") DO (
       SET R_VERSION=%%b
    )
    SET LIB_PATH=%HOMEDRIVE%%HOMEPATH%\mdi-R-library\!R_VERSION!
)
MKDIR %LIB_PATH%

REM -----------------------------------------------------------------------
REM for an installation action, be sure the MDI manager is installed and up to date
REM -----------------------------------------------------------------------
IF "!ACTION_NUMBER!"=="2" (
    "%RSCRIPT%" -e "install.packages('remotes', lib=Sys.getenv('LIB_PATH'), repos = 'https://cloud.r-project.org')" 
    "%RSCRIPT%" -e ".libPaths(Sys.getenv('LIB_PATH')); remotes::install_github('MiDataInt/mdi-manager', lib=Sys.getenv('LIB_PATH'))"  
)

REM -----------------------------------------------------------------------
REM execute the requested MDI command in R
REM -----------------------------------------------------------------------
"%RSCRIPT%" -e ".libPaths(Sys.getenv('LIB_PATH')); mdi::%COMMAND%(%MDI_DIRECTORY%, hostDir=%HOST_DIRECTORY%, %OPTIONS%)"

REM -----------------------------------------------------------------------
REM provide feedback to user once R command exits
REM -----------------------------------------------------------------------
ECHO.
ECHO %MESSAGE%
ECHO.

REM -----------------------------------------------------------------------
REM reset the prompt menu for all actions except 'run'
REM -----------------------------------------------------------------------
IF "!ACTION_NUMBER!"=="1" (
    PAUSE
    ENDLOCAL    
) ELSE (
    GOTO USER_PROMPT
)
