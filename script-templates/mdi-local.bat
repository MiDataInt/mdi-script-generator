ECHO OFF
REM -----------------------------------------------------------------------
REM launch the Michigan Data Interface in local mode on Windows
REM     web server  runs on a user's local desktop/laptop computer
REM     web browser runs on a user's local desktop/laptop computer
REM -----------------------------------------------------------------------
REM this script was generated by XXXX.shinyapps.io
REM GitHub repository for script generator: https://github.com/MiDataInt/mdi-script-generator
REM script template author: Thomas E. Wilson, https://wilsonte-umich.github.io/
REM -----------------------------------------------------------------------

REM -----------------------------------------------------------------------
REM set variable values
REM -----------------------------------------------------------------------
SET MDI_DIRECTORY=__MDI_DIRECTORY__
SET HOST_DIRECTORY=__HOST_DIRECTORY__
SET DATA_DIRECTORY=__DATA_DIRECTORY__
SET R_DIRECTORY=__R_DIRECTORY__
SET INSTALL_PACKAGES=__INSTALL_PACKAGES__
SET SHINY_PORT=__SHINY_PORT__
SET DEVELOPER=__DEVELOPER__

REM -----------------------------------------------------------------------
REM prompt the user for the requested action
REM -----------------------------------------------------------------------
ECHO.
ECHO Welcome to the Michigan Data Interface.
ECHO.
ECHO What would you like to do?
ECHO.
ECHO   1 - run the MDI web interface
ECHO   2 - (re)install the MDI on your computer
ECHO   3 - exit and do nothing
ECHO.
SET /p ACTION_NUMBER=Select an action by its number:

REM -----------------------------------------------------------------------
REM parse the requested action
REM -----------------------------------------------------------------------
IF "%ACTION_NUMBER%"=="1" (
    SET COMMAND=run
    SET OPTIONS=dataDir=%DATA_DIRECTORY%, port=%SHINY_PORT%, debug=%DEVELOPER%, developer=%DEVELOPER%
    SET MESSAGE=MDI shutdown complete
) ELSE IF "%ACTION_NUMBER%"=="2" (
    SET COMMAND=install
    SET OPTIONS=installPackages=%INSTALL_PACKAGES%, confirm=TRUE, addToPATH=FALSE
    SET MESSAGE=MDI installation complete
) ELSE (
    EXIT
)

REM -----------------------------------------------------------------------
REM parse the appropriate R command path
REM -----------------------------------------------------------------------
IF "%R_DIRECTORY%"=="NULL" (
    SET RSCRIPT=Rscript.exe
) ELSE (
    SET RSCRIPT=%R_DIRECTORY%/bin/Rscript.exe
)

REM -----------------------------------------------------------------------
REM execute the requested MDI R command 
REM this is the only action taken by this script on your computer
REM -----------------------------------------------------------------------
"%RSCRIPT%" -e "mdi::%COMMAND%(%MDI_DIRECTORY%, hostDir=%HOST_DIRECTORY%, %OPTIONS%)"

REM -----------------------------------------------------------------------
REM provide feedback to user once R command exits
REM -----------------------------------------------------------------------
ECHO.
ECHO %MESSAGE%
ECHO.

PAUSE
