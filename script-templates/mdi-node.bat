ECHO OFF
SETLOCAL EnableDelayedExpansion
REM ----------------------------------------------------------------
REM launch the MDI web server and browser client in 'node' mode
REM     web server runs on a worker node on a Slurm cluster via an sbatch job on a time limit
REM     web browser runs on a user's local desktop/laptop computer
REM     communication from browser to server is via SSH dynamic port forwarding (i.e., SOCKS5)
REM     thus:
REM         address entered into web browser is "http://NODE:SHINY_PORT"
REM         SwitchyOmega browser extension is used to proxy via "socks5://127.0.0.1:PROXY_PORT"
REM -----------------------------------------------------------------------
REM this script was generated by https://wilsonte-umich.shinyapps.io/mdi-script-generator/
REM GitHub repository for script generator: https://github.com/MiDataInt/mdi-script-generator
REM script template author: Thomas E. Wilson, https://wilsonte-umich.github.io/
REM ----------------------------------------------------------------

REM -----------------------------------------------------------------------
REM set variable values
REM -----------------------------------------------------------------------
SET MDI_DIRECTORY=__MDI_DIRECTORY__
SET HOST_DIRECTORY=__HOST_DIRECTORY__
SET DATA_DIRECTORY=__DATA_DIRECTORY__
SET R_LOAD_COMMAND=__R_LOAD_COMMAND__
SET INSTALL_PACKAGES=__INSTALL_PACKAGES__
SET ADD_TO_PATH=__ADD_TO_PATH__
SET SERVER_URL=__SERVER_URL__
SET USER=__USER__
SET IDENTITY_FILE=__IDENTITY_FILE__
SET SHINY_PORT=__SHINY_PORT__
SET PROXY_PORT=__PROXY_PORT__
SET CLUSTER_ACCOUNT=__CLUSTER_ACCOUNT__
SET JOB_TIME_MINUTES=__JOB_TIME_MINUTES__
SET CPUS_PER_TASK=__CPUS_PER_TASK__
SET MEM_PER_CPU=__MEM_PER_CPU__
SET DEVELOPER=__DEVELOPER__
SET IDENTITY_OPTION=
IF NOT "%IDENTITY_FILE%" = "NULL" (
    SET IDENTITY_OPTION=-i %IDENTITY_FILE%
)

REM -----------------------------------------------------------------------
REM prompt the user for the requested action
REM -----------------------------------------------------------------------
ECHO.
ECHO Welcome to the Michigan Data Interface.
ECHO.
ECHO What would you like to do?
ECHO.
ECHO   1 - run the MDI web interface (local browser, server on cluster node via SSH)
ECHO   2 - (re)install the MDI on the remote server via SSH
ECHO   3 - exit and do nothing
ECHO.
SET /p ACTION_NUMBER=Select an action by its number: 

REM -----------------------------------------------------------------------
REM act on a requested 'run' action
REM executes script 'mdi/remote/mdi-remote-node.sh' on the server computer
REM -----------------------------------------------------------------------
IF "%ACTION_NUMBER%"=="1" (

    REM ssh into server, with dynamic port forwarding (SOCKS5)
    REM launch MDI web server job if one is not already running and report it's access URL
    REM await user input for how to close, including whether or not to leave the web server running after exit
    START "%SERVER_URL%" ssh !IDENTITY_OPTION! -D %PROXY_PORT% %USER%@%SERVER_URL% ^
    bash %MDI_DIRECTORY%/remote/mdi-remote-node.sh ^
    %PROXY_PORT% "%R_LOAD_COMMAND%" %SHINY_PORT% %MDI_DIRECTORY% %DATA_DIRECTORY% %HOST_DIRECTORY% %DEVELOPER% ^
    %CLUSTER_ACCOUNT% %JOB_TIME_MINUTES% %CPUS_PER_TASK% %MEM_PER_CPU% 

    REM open a Chrome browser window that uses the SOCKS5 proxy port (without changing system settings)
    CD "C:\Program Files (x86)\Google\Chrome\Application"
    START "Chrome" chrome.exe 

REM -----------------------------------------------------------------------
REM act on a requested 'install' action
REM -----------------------------------------------------------------------
) ELSE IF "%ACTION_NUMBER%"=="2" (

    REM prompt for installation permission
    SET IP_MESSAGE=-
    IF %INSTALL_PACKAGES%==TRUE (
        SET IP_MESSAGE=- install or update R packages
    )
    SET PATH_MESSAGE=-
    IF %ADD_TO_PATH%==TRUE (
        SET PATH_MESSAGE=- modify '~/.bashrc' to add the mdi executable to PATH
    )
    ECHO.
    ECHO ------------------------------------------------------------------
    ECHO PLEASE CONFIRM MDI INSTALLATION ACTIONS
    ECHO ------------------------------------------------------------------
    ECHO.
    ECHO   - install the R MDI manager package
    ECHO   - populate %SERVER% directory %MDI_DIRECTORY%
    ECHO   - clone or update MDI repositories from GitHub
    ECHO   - check out the most recent version of all definitive MDI repositories
    ECHO   !IP_MESSAGE!
    ECHO   !PATH_MESSAGE!
    ECHO.
    SET /p CONFIRMATION=Do you wish to continue? [type 'y' for 'yes']: 

    REM ssh into server and execute the installation
    IF "!CONFIRMATION!"=="y" (
        ssh !IDENTITY_OPTION! %USER%@%SERVER_URL% ^
        %R_LOAD_COMMAND%; ^
        Rscript -e """install.packages('remotes', repos = 'https://cloud.r-project.org')"""; ^
        Rscript -e """remotes::install_github('MiDataInt/mdi-manager')"""; ^
        Rscript -e """mdi::install('%MDI_DIRECTORY%', hostDir='%HOST_DIRECTORY%', installPackages = %INSTALL_PACKAGES%, confirm = FALSE, addToPATH = %ADD_TO_PATH%)"""; ^
        echo; ^
        echo "Done"
        REM 
        PAUSE
    )
)

ENDLOCAL
