ECHO OFF
SETLOCAL EnableDelayedExpansion
REM ----------------------------------------------------------------
REM launch the MDI web server and browser client in 'node' mode
REM     web server runs on a worker node on a Slurm cluster via an sbatch job on a time limit
REM     web browser runs on a user's local desktop/laptop computer
REM     communication from browser to server is via SSH dynamic port forwarding (i.e., SOCKS5)
REM     thus:
REM         address entered into web browser is "http://NODE:SHINY_PORT"
REM         SwitchyOmega browser extension is used to proxy via "socks5://127.0.0.1:PROXY_PORT"
REM -----------------------------------------------------------------------
REM this script was generated by https://wilsonte-umich.shinyapps.io/mdi-script-generator/
REM GitHub repository for script generator: https://github.com/MiDataInt/mdi-script-generator
REM script template author: Thomas E. Wilson, https://wilsonte-umich.github.io/
REM ----------------------------------------------------------------

REM -----------------------------------------------------------------------
REM set variable values
REM -----------------------------------------------------------------------
SET MDI_DIRECTORY=__MDI_DIRECTORY__
SET HOST_DIRECTORY=__HOST_DIRECTORY__
SET DATA_DIRECTORY=__DATA_DIRECTORY__
SET R_LOAD_COMMAND=__R_LOAD_COMMAND__
SET R_LOAD_COMMAND_MASKED=__R_LOAD_COMMAND_MASKED__
SET INSTALL_PACKAGES=__INSTALL_PACKAGES__
SET INSTALL_N_CPU=__INSTALL_N_CPU__
SET SERVER_URL=__SERVER_URL__
SET USER=__USER__
SET IDENTITY_FILE=__IDENTITY_FILE__
SET SHINY_PORT=__SHINY_PORT__
SET PROXY_PORT=__PROXY_PORT__
SET CLUSTER_ACCOUNT=__CLUSTER_ACCOUNT__
SET JOB_TIME_MINUTES=__JOB_TIME_MINUTES__
SET CPUS_PER_TASK=__CPUS_PER_TASK__
SET MEM_PER_CPU=__MEM_PER_CPU__
SET DEVELOPER=__DEVELOPER__

REM -----------------------------------------------------------------------
REM prompt the user for the requested action
REM -----------------------------------------------------------------------
:USER_PROMPT
SET ACTION_NUMBER=
ECHO.
ECHO Welcome to the Michigan Data Interface.
ECHO.
ECHO   %USER%@%SERVER_URL%:%SHINY_PORT%
ECHO   MDI_DIRECTORY    %MDI_DIRECTORY%
ECHO   HOST_DIRECTORY   %HOST_DIRECTORY%
ECHO   DATA_DIRECTORY   %DATA_DIRECTORY%
ECHO   R_LOAD_COMMAND   %R_LOAD_COMMAND%
ECHO   INSTALL_N_CPU    %INSTALL_N_CPU%
ECHO   CLUSTER_ACCOUNT  %CLUSTER_ACCOUNT%
ECHO   JOB_TIME_MINUTES %JOB_TIME_MINUTES%
ECHO   CPUS_PER_TASK    %CPUS_PER_TASK%
ECHO   MEM_PER_CPU      %MEM_PER_CPU%
ECHO   DEVELOPER        %DEVELOPER%
ECHO.
ECHO What would you like to do?
ECHO.
ECHO   1 - (re)install the MDI on the remote server via SSH
ECHO   2 - use nano to edit one of the server configuration files
ECHO   3 - run the MDI web interface (local browser, server on cluster node via SSH)
ECHO   4 - bring up an interactive bash terminal on the server
ECHO   5 - exit and do nothing
ECHO.
SET /p ACTION_NUMBER=Select an action by its number: 

REM -----------------------------------------------------------------------
REM act on a requested 'install' action
REM -----------------------------------------------------------------------
IF "!ACTION_NUMBER!"=="1" (

    REM prompt for installation permission
    SET IP_MESSAGE=-
    IF %INSTALL_PACKAGES%==TRUE (
        SET IP_MESSAGE=- install or update R packages
    )
    ECHO.
    ECHO ------------------------------------------------------------------
    ECHO PLEASE CONFIRM MDI INSTALLATION ACTIONS
    ECHO ------------------------------------------------------------------
    ECHO.
    ECHO   - create and populate directory %MDI_DIRECTORY% on %SERVER_URL%
    ECHO   - install the MDI manager repository
    ECHO   - clone or update framework and suite repositories from GitHub
    ECHO   - check out the most recent version of all definitive repositories
    ECHO   !IP_MESSAGE!
    ECHO.
    SET /p CONFIRMATION=Do you wish to continue? [type 'y' for 'yes']: 

    REM ssh into server and execute the installation
    IF "!CONFIRMATION!"=="y" (
        SET IP_FLAG=
        IF %INSTALL_PACKAGES%==TRUE (
            SET IP_FLAG=--install-packages
        )
        SET FORKS_FLAG=
        IF %DEVELOPER%==TRUE (
            SET FORKS_FLAG=--forks
        )
        ssh !IDENTITY_FILE! -o "StrictHostKeyChecking no" %USER%@%SERVER_URL% ^
        %R_LOAD_COMMAND%; ^
        export SUPPRESS_MDI_BASHRC=TRUE; ^
        if [ ^^! -d %MDI_DIRECTORY% ]; then mkdir -p %MDI_DIRECTORY%; fi; ^
        cd %MDI_DIRECTORY%; ^
        if [ ^^! -e install.sh ]; then git clone https://github.com/MiDataInt/mdi.git .; fi; ^
        if [ ^^! -e mdi ]; then ./install.sh 1; fi; ^
        ./mdi install !IP_FLAG! !FORKS_FLAG! --n-cpu %INSTALL_N_CPU%; ^
        echo; ^
        echo "Done"
        REM 
        PAUSE
    )

REM -----------------------------------------------------------------------
REM request the server file to edit
REM -----------------------------------------------------------------------
) ELSE IF "!ACTION_NUMBER!"=="2" (
    ECHO.
    ECHO Please select the server file you would like to edit.
    ECHO.
    ECHO   1 - suites.yml            pipelines and apps suites to install
    ECHO   2 - stage1-pipelines.yml  system defaults for pipeline execution
    ECHO   3 - stage2-apps.yml       access control options for the apps server
    ECHO   4 - singularity.yml       optional command to load Singularity
    ECHO   5 - exit and do nothing
    ECHO.
    SET /p FILE_NUMBER=Select a file to edit by its number: 
    
    IF "!FILE_NUMBER!"=="1" (
        SET FILE_NAME=suites.yml
    ) ELSE IF "!FILE_NUMBER!"=="2" (
        SET FILE_NAME=stage1-pipelines.yml
    ) ELSE IF "!FILE_NUMBER!"=="3" (
        SET FILE_NAME=stage2-apps.yml
    ) ELSE IF "!FILE_NUMBER!"=="4" (
        SET FILE_NAME=singularity.yml
    ) ELSE (
        ENDLOCAL
        EXIT
    )
    ssh !IDENTITY_FILE! -o "StrictHostKeyChecking no" %USER%@%SERVER_URL% -t nano %MDI_DIRECTORY%/config/!FILE_NAME!

REM -----------------------------------------------------------------------
REM act on a requested 'run' action
REM executes script 'mdi/remote/mdi-remote-node.sh' on the server computer
REM -----------------------------------------------------------------------
) ELSE IF "!ACTION_NUMBER!"=="3" (

    REM ssh into server, with dynamic port forwarding (SOCKS5)
    REM launch MDI web server job if one is not already running and report it's access URL
    REM await user input for how to close, including whether or not to leave the web server running after exit
    ssh -t !IDENTITY_FILE! -o "StrictHostKeyChecking no" -D %PROXY_PORT% %USER%@%SERVER_URL% ^
    bash %MDI_DIRECTORY%/remote/mdi-remote-node.sh ^
    %PROXY_PORT% %R_LOAD_COMMAND_MASKED% %SHINY_PORT% %MDI_DIRECTORY% %DATA_DIRECTORY% %HOST_DIRECTORY% %DEVELOPER% ^
    %CLUSTER_ACCOUNT% %JOB_TIME_MINUTES% %CPUS_PER_TASK% %MEM_PER_CPU%

REM -----------------------------------------------------------------------
REM ssh into the server as per normal
REM -----------------------------------------------------------------------
) ELSE IF "!ACTION_NUMBER!"=="4" (
    ssh !IDENTITY_FILE! -o "StrictHostKeyChecking no" %USER%@%SERVER_URL%
) ELSE (
    ENDLOCAL
    EXIT
)

REM -----------------------------------------------------------------------
REM reset the prompt menu for all actions except 'run'
REM -----------------------------------------------------------------------
IF "!ACTION_NUMBER!"=="3" (
    ENDLOCAL    
) ELSE (
    GOTO USER_PROMPT
)
