ECHO OFF
SETLOCAL EnableDelayedExpansion
REM ----------------------------------------------------------------
REM launch the MDI web server and browser client in 'public' mode
REM     web server  runs on a remote AWS EC2 instance in the cloud
REM     web browser runs on a user's local desktop/laptop computer
REM     thus, address entered into web browser is "https://example.org"
REM -----------------------------------------------------------------------
REM this script was generated by XXXX.shinyapps.io
REM GitHub repository for script generator: https://github.com/MiDataInt/mdi-script-generator
REM script template author: Thomas E. Wilson, https://wilsonte-umich.github.io/
REM ----------------------------------------------------------------

REM -----------------------------------------------------------------------
REM set variable values
REM -----------------------------------------------------------------------
SET SERVER_URL=__SERVER_URL__

REM -----------------------------------------------------------------------
REM prompt the user for the requested action
REM -----------------------------------------------------------------------
ECHO.
ECHO Welcome to the Michigan Data Interface.
ECHO.
ECHO What would you like to do to the server at %SERVER_URL%?
ECHO.
ECHO   Server execution commands (in order of usage):
ECHO     build     run docker-compose build to create all needed Docker images
ECHO     install   update the server config, clone GitHub repos, install R packages
ECHO     up        launch all containers to run the MDI apps server
ECHO     down      stop and remove any running containers to shut down the apps server
ECHO.
ECHO   Additional resource management commands:
ECHO     edit      use nano to edit one of the server configuration files
ECHO     bash      bring up an interactive bash terminal in a new apps-server container
ECHO.
SET /p ACTION_NAME=Please type a command name (Enter to do nothing): 

REM exit and do nothing
IF "%ACTION_NAME%"=="" (
    ENDLOCAL
    EXIT

REM request the file to edit
) ELSE IF "%ACTION_NAME%"=="edit" (
    ECHO.
    ECHO Please select the server file you would like to edit.
    ECHO.
    ECHO   1 - server.sh             server configuration metadata
    ECHO   2 - suites.yml            pipelines and apps suites to install
    ECHO   3 - stage1-pipelines.yml  system defaults for pipeline execution
    ECHO   4 - stage2-apps.yml       access control options for the apps server
    ECHO   5 - exit and do nothing
    ECHO.
    SET /p FILE_NUMBER=Select a file to edit by its number: 
    
    IF "!FILE_NUMBER!"=="1" (
        SET FILE_NAME=server.sh
    ) ELSE IF "!FILE_NUMBER!"=="2" (
        SET FILE_NAME=suites.yml
    ) ELSE IF "!FILE_NUMBER!"=="3" (
        SET FILE_NAME=stage1-pipelines.yml
    ) ELSE IF "!FILE_NUMBER!"=="4" (
        SET FILE_NAME=stage2-apps.yml
    ) ELSE (
        ENDLOCAL
        EXIT
    )
    ssh ubuntu@%SERVER_URL% /srv/mdi/server edit !FILE_NAME!

REM send all other actions directly to server via SSH
) ELSE (
    ssh ubuntu@%SERVER_URL% /srv/mdi/server %ACTION_NAME%
)

ENDLOCAL

PAUSE
